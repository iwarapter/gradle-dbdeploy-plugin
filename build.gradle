apply plugin: 'groovy'
apply plugin: 'maven'


description 'Gradle plugin for the lightweight dbdeploy database change management tool.'
group 'com.lv.plugins'
sourceCompatibility = 1.5
version = 1.1

repositories {
    if( project.hasProperty('internalArtifactRepository') ){
        maven { url project.internalArtifactRepository }
    } else {
        mavenCentral()
        jcenter()
    }
}

dependencies{
    compile gradleApi()
    compile localGroovy()
    compile 'com.dbdeploy:dbdeploy-core:3.0M3'

    testCompile 'com.netflix.nebula:nebula-test:1.12.+'
}

sourceSets {
    integTest {
        groovy.srcDir file('src/integTest/groovy')
        resources.srcDir file('src/integTest/resources')
        compileClasspath = sourceSets.main.output + configurations.testRuntime
        runtimeClasspath = output + compileClasspath
    }
}

task integTest(type: Test) {
    testClassesDir = sourceSets.integTest.output.classesDir
    classpath = sourceSets.integTest.runtimeClasspath
}

integTest.mustRunAfter test
check.dependsOn integTest

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://${project.rootDir}/demo/libs")
        }
    }
}